plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
}

sponge {
    plugin {
        meta {
            id = project.ext.id.toLowerCase()
            version = project.ext.version
            description = project.ext.description
        }
    }
}

repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    api project(":core")

    implementation fileTree(dir: "libs", includes: ['*.jar'])
    implementation 'org.spongepowered:spongeapi:7.4.0'

    testImplementation project(':core').sourceSets.test.output
}

jar {
    from {
        String[] include = [
                "${project(':core').archivesBaseName}-${project.version}.jar",
                "commons-lang3-3.12.0.jar"
        ]

        configurations.runtimeClasspath
                .findAll { include.contains(it.name) }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }

    dependsOn project(':core').jar
}